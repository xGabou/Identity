plugins {
    id 'com.gradleup.shadow'
    id 'com.matthewprenger.cursegradle' version '1.4.0' // CurseForge uploader
    id "com.modrinth.minotaur" version "2.8.10" // Modrinth uploader
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    modImplementation "dev.architectury:architectury-neoforge:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    modImplementation "curse.maven:gabous-libs-1367332:7183487"
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

// === Génération du mods.toml ===
tasks.register("generateModMetadata", ProcessResources) {
    def replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: "[${minecraft_version},)",
            neo_version            : neoforge_version,
            neo_version_range      : "[${neoforge_version},)",
            loader_version_range   : neoforge_version_range,

            mod_id          : mod_id,
            mod_name        : mod_name,
            mod_license     : mod_license,
            mod_version     : mod_version,
            mod_authors     : mod_authors,
            mod_description : mod_description
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from("src/main/templates")
    into("build/generated/sources/modMetadata")
}

// inclure le fichier généré comme ressource
sourceSets.main.resources.srcDir(tasks.named("generateModMetadata"))

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "u8V1OwVf" // from your Modrinth project URL
    versionNumber = "NeoForge-${project.version}"
    versionName = "Identity ${mod_version}"
    changelog = rootProject.file("IdentityChangeLog.md").text
    gameVersions = ["1.21.1"]
    loaders = ["neoforge"]
    versionType = "release"
    dependencies {
        required.project "lhGA9TYQ"
    }
    uploadFile = file("build/libs/${archivesBaseName}-${version}.jar")
}
curseforge {
    apiKey = System.getenv("CURSEFORGE_TOKEN")

    project {
        id = "1238155"
        changelog = rootProject.file("IdentityChangeLog.md")
        releaseType = "release" // options: alpha, beta, release
        addGameVersion "1.21.1"
        addGameVersion "NeoForge"
        mainArtifact(file("build/libs/${archivesBaseName}-${version}.jar")) {
            displayName = "NeoForge-${version}"
        }
    }
}