plugins {
    id 'com.gradleup.shadow'
    id 'com.matthewprenger.cursegradle' version '1.4.0' // CurseForge uploader
    id "com.modrinth.minotaur" version "2.8.10" // Modrinth uploader
}

loom {
    forge {
        mixinConfigs = [
                "identity-forge.mixins.json",
                "identity.mixins.json",
                "identity.alexsmobs.mixins.json"
        ]
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"
    modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"

    // Forge-only optional dependencies for compilation
    modCompileOnly "curse.maven:alexs-mobs-426558:5698791"
    modCompileOnly "curse.maven:citadel-331936:5633260"

    // Forge-only runtime dependencies for development
    modRuntimeOnly "curse.maven:alexs-mobs-426558:5698791"
    modRuntimeOnly "curse.maven:citadel-331936:5633260"
    //modRuntimeOnly "curse.maven:sculk-horde-501766:6893408"
    modImplementation "curse.maven:gabous-libs-1367332:7160159"
//    modCompileOnly "curse.maven:naturalist-627986:6509918"
//    modRuntimeOnly "curse.maven:naturalist-627986:6509918"
    modCompileOnly("com.starfish_studios:naturalist-5.0pre4+forge-1.20.1")
    modRuntimeOnly("com.starfish_studios:naturalist-5.0pre4+forge-1.20.1")
    modRuntimeOnly("software.bernie.geckolib:geckolib-forge-${minecraft_version}:${geckolib_version}")
    modCompileOnly("software.bernie.geckolib:geckolib-forge-${minecraft_version}:${geckolib_version}")
    modImplementation include ("maven.modrinth:midnightlib:${project.midnightlib_forge_version}-forge")
    forgeRuntimeLibrary("com.eliotlash.mclib:mclib:20")

    common(project(path: ":common", configuration: "namedElements")) { transitive = false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }
}

processResources {
    inputs.property "version", project.version.toString()

    filesMatching("META-INF/mods.toml") {
        expand("version": project.version.toString())
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev-shadow")
    exclude("fabric.mod.json")
}

remapJar {
    input.set(shadowJar.archiveFile)
    dependsOn(shadowJar)
    archiveClassifier.set("forge")
}

jar {
    archiveClassifier.set("dev")
}

sourcesJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    def commonSources = project(":common").sourcesJar
    dependsOn(commonSources)
    from(commonSources.archiveFile.map { zipTree(it) })
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }
}
modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "u8V1OwVf" // from your Modrinth project URL
    versionNumber = "Forge-${project.version}"
    versionName = "Identity ${mod_version}"
    changelog = rootProject.file("IdentityChangeLog.md").text
    gameVersions = ["1.20.1"]
    loaders = ["forge"]
    versionType = "release"
    dependencies {
        required.project "lhGA9TYQ"
    }
    uploadFile = file("build/libs/${archivesBaseName}-${version}-forge.jar")
}
curseforge {
    apiKey = System.getenv("CURSEFORGE_TOKEN")

    project {
        id = "1238155"
        changelog = rootProject.file("IdentityChangeLog.md")
        releaseType = "release" // options: alpha, beta, release
        addGameVersion "1.20.1"
        addGameVersion "Forge"
        mainArtifact(file("build/libs/${archivesBaseName}-${version}-forge.jar")) {
            displayName = "Forge-${version}"
        }
    }
}
