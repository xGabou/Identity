import groovy.json.JsonOutput

plugins {
    id 'dev.architectury.loom' version '1.11-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.gradleup.shadow' version '8.3.6' apply false
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base {
        // Set up a suffixed format for the mod jar names, e.g. `example-fabric`.
        archivesName = "$rootProject.archives_name-$project.name"
    }

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        maven {
            name = "CurseMaven"
            url = "https://cursemaven.com"
        }
        maven {
            url = "https://api.modrinth.com/maven"
        }
        maven {
            name = 'GeckoLib'
            url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
            content {
                includeGroupByRegex("software\\.bernie.*")
                includeGroup("com.eliotlash.mclib")
            }
        }
        maven {
            name = "Terraformers"
            url = "https://maven.terraformersmc.com/"
        }
        maven { url "https://maven.shedaniel.me/" }
        flatDir { dir 'libs' }
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.layered {
            it.mappings("net.fabricmc:yarn:$rootProject.yarn_mappings:v2")
            it.mappings("dev.architectury:yarn-mappings-patch-neoforge:$rootProject.yarn_mappings_patch_neoforge_version")
        }
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()

        // Use Java 21 via Gradle toolchains so builds work even if the
        // system default Java is older (e.g., Java 17 on some setups).
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    // Configure Maven publishing.
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }

        // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
        repositories {
            // Add repositories to publish to here.
            // Notice: This block does NOT have the same function as the block in the top level.
            // The repositories here will be used for publishing your artifact, not for
            // retrieving dependencies.
        }
    }
}
tasks.register("discordWebhook") {
    doLast {
        def webhookUrl = System.getenv("DISCORD_WEBHOOK_URL")
        def changelogText = rootProject.file("IdentityChangeLog.md").text

        def payload = [
                username: "Build Bot",
                allowed_mentions: [ parse: ["roles"] ],
                content: "**New release published!** ðŸŽ‰\n" +
                        "<@&1363945953546997810>\n" + // role mention
                        "Project: ${mod_name}\n" +
                        "Version: v${version}-1.21.1\n\n" +
                        "${changelogText.take(1900)}"
        ]

        println "Sending release notification to Discord..."
        def connection = new URL(webhookUrl).openConnection()
        connection.with {
            doOutput = true
            requestMethod = "POST"
            setRequestProperty("Content-Type", "application/json")
            outputStream.withWriter("UTF-8") { it << JsonOutput.toJson(payload) }
            println "Webhook sent with response code: ${responseCode}"
        }
    }
}
