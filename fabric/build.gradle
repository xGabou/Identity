plugins {
    id 'com.gradleup.shadow'
    id 'com.matthewprenger.cursegradle' version '1.4.0' // CurseForge uploader
    id "com.modrinth.minotaur" version "2.8.10" // Modrinth uploader
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

repositories {
    maven {
        url "https://maven.draylar.dev/releases"
    }
    maven {
        url "https://maven.terraformersmc.com/releases"
    }

}


dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    modApi "dev.architectury:architectury-fabric:${rootProject.architectury_version}"
    modImplementation "curse.maven:gabous-libs-1367332:7163327"

    //modImplementation fernflowerDecompilerClasspath("curse.maven:minecraft-comes-alive-reborn-535291:6805928")

    common(project(path: ":common", configuration: "namedElements")) { transitive = false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive = false }
    modImplementation "com.terraformersmc:modmenu:${rootProject.modmenu_version}"
    modImplementation include("me.shedaniel.cloth:cloth-config-fabric:${rootProject.cloth_config_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }

    from 'LICENSE'
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = "fabric"
}

jar {
    archiveClassifier = "dev"
}

sourcesJar {
    duplicatesStrategy 'exclude'
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "u8V1OwVf" // from your Modrinth project URL
    versionNumber = "Fabric-${project.version}"
    versionName = "Identity ${mod_version}"
    changelog = rootProject.file("IdentityChangeLog.md").text
    gameVersions = ["1.20.1"]
    loaders = ["fabric"]
    versionType = "release"
    dependencies {
        required.project "mOgUt4GM"
        required.project "lhGA9TYQ"
    }
    uploadFile = file("build/libs/${archivesBaseName}-${version}-fabric.jar")
}
curseforge {
    apiKey = System.getenv("CURSEFORGE_TOKEN")

    project {
        id = "1238155"
        changelog = rootProject.file("IdentityChangeLog.md")
        releaseType = "release" // options: alpha, beta, release
        addGameVersion "1.20.1"
        addGameVersion "Fabric"
        mainArtifact(file("build/libs/${archivesBaseName}-${version}-fabric.jar")) {
            displayName = "Fabric-${version}"
        }
    }
}
